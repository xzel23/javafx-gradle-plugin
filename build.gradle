plugins {
    id 'java-gradle-plugin'
    id 'com.github.hierynomus.license' version '0.15.0'
    id 'com.gradle.plugin-publish' version '0.12.0'
    id 'com.github.ben-manes.versions' version '0.20.0'
    id 'maven-publish'
}

group 'com.dua3.gradle'
version '0.0.9'

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenCentral()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

dependencies {
    implementation gradleApi()

    implementation 'com.google.gradle:osdetector-gradle-plugin:1.7.0'

    testImplementation gradleTestKit()
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.2'
}

test {
    useJUnitPlatform()
}

gradlePlugin {
    plugins {
        javafxPlugin {
            id = 'com.dua3.javafxgradle7plugin'
            displayName = 'JavaFX Gradle 7 Plugin'
            description = 'Plugin that makes it easy to work with JavaFX and Gradle 7+ (JDK 16+)'
            implementationClass = 'org.openjfx.gradle.JavaFXPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/xzel23/javafx-gradle-plugin'
    vcsUrl = 'https://github.com/xzel23/javafx-gradle-plugin.git'
    tags = [ 'java', 'javafx' ]
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = 'javafx-gradle7-plugin'
            version = project.version

            from components.java
        }
    }
    repositories {
        maven {
            if (project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
                credentials {
                    username project.property('sonatypeUsername')
                    password project.property('sonatypePassword')
                }
                url = "https://oss.sonatype.org/content/repositories/snapshots/"
            }
        }
    }
}

license {
    skipExistingHeaders = true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}
